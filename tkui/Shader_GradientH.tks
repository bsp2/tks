// ----
// ---- file   : Shader_GradientH.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2024-2025 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "ui" toolkit.
// ----
// ---- created: 23Mar2024
// ---- changed: 28Mar2024, 13Sep2025
// ----
// ----
// ----

module MShader_GradientH;

namespace ui;


class Shader_GradientH : Shader {
   protected Texture *tex;

   // Inputs:
   //   tex[0] : Texture (GL_TEXTURE_2D)

   static String vs_src =
      "uniform mat4 u_transform; \n"
      " \n"
      "ATTRIBUTE vec2 a_vertex; \n"
      " \n"
      "void main(void) { \n"
      "  gl_Position = u_transform * vec4(a_vertex,0,1); \n"
      "}"
      ;

   static String fs_src =
      "uniform sampler2D u_tex; \n"
      "uniform vec4      u_color_fill; \n"
      "uniform float     u_abs_l; \n"
      "uniform float     u_abs_r; \n"
      "uniform float     u_abs_s; \n"  // 1.0f / (r - l)
      "\n"
      "void main(void) { \n"
      "  float u = 1.0 - (gl_FragCoord.x - u_abs_l) * u_abs_s; \n"
      "  vec4  c = TEXTURE2D(u_tex, vec2(u, 0)); \n"
      "  c = vec4(mix(u_color_fill.rgb, c.rgb, c.a), u_color_fill.a); \n"
      "  FRAGCOLOR = c; \n"
      "}"
      ;

   public virtual create() : boolean {
      if(createFromStrings(vs_src, fs_src))
      {
         sdvg_BindShader(sdvg_shader_idx);
         sdvg_Uniform1i("u_tex", 0);
         sdvg_BindShader(0);
         if(Configuration.debugLevel >= 2)
            trace "[dbg] ui::Shader_GradientH::create: OK.";
         return true;
      }
      else
      {
         trace "[!!!] ui::Shader_GradientH::create: failed";
      }
      return false;
   }

   public method bindGradientH(Texture _tex, float _absL, float _absR) {
      tex <= _tex;
      tex.bind();
      tex.enable();
      float absS = 1.0f / (_absR - _absL);
      sdvg_BindShader(sdvg_shader_idx);
      sdvg_Uniform1f("u_abs_l", _absL);
      sdvg_Uniform1f("u_abs_r", _absR);
      sdvg_Uniform1f("u_abs_s",  absS);
      // trace "xxx Shader_GradientH: texSz=("+tex.sx+";"+tex.sy+") UI.buffer=("+UI.buffer_width+";"+UI.buffer_height+") absL="+_absL+" absR="+_absR+" absS="+absS;
   }

   public virtual unbind() {
      Shader::unbind();
      if(null != tex)
      {
         tex.disable();
         tex.unbind();
      }
   }

   public virtual onOpen() {
      Shader::onOpen();
      if(null != tex)
         tex.unload();
   }

}
