// ----
// ---- file   : Action.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2005-2023 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   :
// ----
// ---- changed: 19Apr2005, 20Apr2005, 28May2005, 07Jun2005, 09Jun2005, 10Jun2005, 18Jun2005
// ----          23Jun2005, 24Jun2005, 25Jun2005, 17Jul2005, 10Aug2005, 13Aug2005, 14Aug2005
// ----          10Jan2006, 12Aug2006, 28Apr2007, 29Apr2007, 30Apr2007, 05May2007, 30Oct2007,
// ----          11Nov2007, 02Feb2009, 27Oct2020, 30Sep2022, 05Nov2023
// ----
// ----
// ----

module MAction;

namespace ui;


class Action {
   explain "Base class for ui events.
Actions are emitted by ActionProviders and handled by ActionConsumers.";

   public String name;
   protected ActionProvider *provider;

   public function New(String _name, ActionProvider _provider) : Action {
      local Action ac;
      ac.name      = _name;
      ac.provider <= _provider;
      return deref ac;
   }

   public method setActionName(String _name) {
      explain "Set the name of this action.";
      name = _name;
   }

   public method getActionName() : String {
      explain "Query the name of this action.";
      return name;
   }

   public method setActionProvider(ActionProvider _provider) {
      explain "Set the ActionProvider which emitted this action.";
      provider <= _provider;
   }

   public method getActionProvider() /*: ActionProvider*/ {
      explain "Query the ActionProvider which emitted this action.";
      if(null == provider)
         return ActionProvider;  // never return null
      return provider;
   }
}
