// ----
// ---- file   : PopupMenuCheckButton.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2007-2023 by Bastian Spiegel. 
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See 
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "ui" toolkit.
// ----
// ---- created: 14May2007
// ---- changed: 20May2010, 31May2013, 01Jun2013, 22Dec2021, 20Oct2023
// ----          
// ----
// ----

module MPopupMenuCheckButton;

namespace ui;


class PopupMenuCheckButton extends PopupMenuButton {
   explain "XFM interface class that represents a popupmenu checkbox/radiobutton item.";

   public boolean b_radio;

   protected boolean b_selected;


   public method setSelected(boolean _bSelected) {
      String iconName = b_radio ? (_bSelected ? "rab_1" : "rab_0") : ( (maybe == _bSelected) ? "chb_2" : _bSelected ? "chb_1" : "chb_0");
         
      b_selected = _bSelected;

      Icon ico <= UI.GetIcon(iconName);
      if(null != ico)
      {
         setIcon(ico);
         setIconPlacement(Layout.LEFT|Layout.CENTERY);
         setIconPadRight(UIConstants.POPUPMENU_ICON_PAD_R);
         setInnerPadLeft(UIConstants.BUTTON_INSET_LEFT);
         // if(UI.IsHiDPI())
         //    setIconPadBottom(4);
      }
      else
      {
         trace "[---] PopupMenuCheckButton: GetIcon("+iconName+") failed.";
      }
   }

   public method setChecked(boolean _bChecked) { // **DEPRECATED**
      setSelected(_bChecked);
   }

   public method isSelected() : boolean {
      return b_selected;
   }

   public virtual beginXFMTag(AbstractXMLForm _form, HashTable _attributes) : boolean {

      if(PopupMenuButton::beginXFMTag(_form, _attributes))
      {
         boolean bSelected = false;
         String atname, atval, atvaluc;
         StringArray atsplit;
         foreach atname in _attributes
         {
            atval <= _attributes[atname];
            switch(atname)
            {
               case "radio":
                  b_radio = (atval ? true : false);
                  break;

               case "checked": // **DEPRECATED**
               case "selected":
                  bSelected = atval;
                  break;
            }
         }

         setSelected(bSelected);

         lockInnerPadding();

         return true;
      }
      return false;
   }

}


class PopupMenuRadioButton : PopupMenuCheckButton {

   public virtual beginXFMTag(AbstractXMLForm _form, HashTable _attributes) : boolean {

      b_radio = true;

      return PopupMenuCheckButton::beginXFMTag(_form, _attributes);
   }
}
