// ----
// ---- file   : Shader_GradientV.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2024-2025 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "ui" toolkit.
// ----
// ---- created: 23Mar2024
// ---- changed: 28Mar2024, 13Sep2025
// ----
// ----
// ----

module MShader_GradientV;

namespace ui;


class Shader_GradientV : Shader {
   protected Texture *tex;

   // Inputs:
   //   tex[0] : Texture (GL_TEXTURE_2D)

   static String vs_src =
      "uniform mat4 u_transform; \n"
      " \n"
      "ATTRIBUTE vec2 a_vertex; \n"
      " \n"
      "void main(void) { \n"
      "  gl_Position = u_transform * vec4(a_vertex,0,1); \n"
      "}"
      ;

   static String fs_src =
      "uniform sampler2D u_tex; \n"
      "uniform vec4      u_color_fill; \n"
      "uniform float     u_abs_t; \n"
      "uniform float     u_abs_b; \n"
      "uniform float     u_abs_s; \n"  // 1.0f / (t - b)
      "\n"
      "void main(void) { \n"
      "  float u = 1.0 - (gl_FragCoord.y - u_abs_b) * u_abs_s; \n"
      "  vec4  c = TEXTURE2D(u_tex, vec2(u, 0)); \n"
      "  c = vec4(mix(u_color_fill.rgb, c.rgb, c.a), u_color_fill.a); \n"
      "  FRAGCOLOR = c; \n"
      "}"
      ;

   public virtual create() : boolean {
      if(createFromStrings(vs_src, fs_src))
      {
         sdvg_BindShader(sdvg_shader_idx);
         sdvg_Uniform1i("u_tex", 0);
         sdvg_BindShader(0);
         if(Configuration.debugLevel >= 2)
            trace "[dbg] ui::Shader_GradientV::create: OK.";
         return true;
      }
      else
      {
         trace "[!!!] ui::Shader_GradientV::create: failed";
      }
      return false;
   }

   public method bindGradientV(Texture _tex, float _absT, float _absB) {
      // (note) absT > absB (gl_FragCoord window origin 0;0 is bottom/left)
      tex <= _tex;
      tex.bind();
      tex.enable();
      Shader::bind();
      float absS = 1.0f / (_absT - _absB);
      sdvg_Uniform1f("u_abs_t", _absT);
      sdvg_Uniform1f("u_abs_b", _absB);
      sdvg_Uniform1f("u_abs_s",  absS);
      // trace "xxx Shader_GradientV: texSz=("+tex.sx+";"+tex.sy+") UI.buffer=("+UI.buffer_width+";"+UI.buffer_height+") absT="+_absT+" absB="+_absB+" absS="+absS;
   }

   public virtual unbind() {
      Shader::unbind();
      if(null != tex)
      {
         tex.disable();
         tex.unbind();
      }
   }

   public virtual onOpen() {
      Shader::onOpen();
      if(null != tex)
         tex.unload();
   }

}
