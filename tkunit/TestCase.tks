// ----
// ---- file   : TestCase.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2008 by Bastian Spiegel. 
// ----          Distributed under terms of the GNU GENERAL PUBLIC LICENSE (GPL). See 
// ----          http://www.gnu.org/licenses/licenses.html#GPL or COPYING for further information.
// ----
// ---- info   : This is part of the "tkunit" package.
// ----
// ---- changed: 07Feb2008, 10Feb2008, 11Feb2008
// ----          
// ----          
// ----
// ----

module MTestCase;


define exception TKUnitException : CriticalError;
define exception TKUnitTestIgnoredException : TKUnitException;
define exception TKUnitAssertionFailedError : TKUnitException;
define exception TKUnitBooleanAssertionFailedError : TKUnitAssertionFailedError;
define exception TKUnitIntegerAssertionFailedError : TKUnitAssertionFailedError;
define exception TKUnitFloatAssertionFailedError   : TKUnitAssertionFailedError;
define exception TKUnitObjectAssertionFailedError  : TKUnitAssertionFailedError;
define exception TKUnitTypeAssertionFailedError    : TKUnitAssertionFailedError;


class TestCase {

   public method pre() {
      explain "Set up test fixture. A test fixture is a set of conditions shared by all test*() methods in this test case.";
   }
   
   public method post() {
      explain "Tear down test fixture.";
   }

   protected method ignoreq() {
      throw TKUnitTestIgnoredException("");
   }

   protected method ignore(String _reason) {
      explain "Should be used whenever a test shall be ignored. Ignored tests still show up in the final report so that they can not be forgotten so easily.";

      throw TKUnitTestIgnoredException(_reason);
   }

   protected method assertq(boolean _required, boolean _have) {
      explain "Check whether _have equals _required. Raise a TKUnitBooleanAssertionFailedError if the check fails.";

      assert(_required, _have, "");
   }

   protected method assert(boolean _required, boolean _have, String _msg) {
      explain "Check whether _have equals _required. Raise a TKUnitBooleanAssertionFailedError if the check fails.";

      if(_have != _required)
      {
         throw TKUnitBooleanAssertionFailedError(_msg+"("+(_have?"true":"false")+" != "+(_required?"true":"false")+")");
      }
   }

   protected method assertqInt(int _required, int _have) {
      explain "Check whether _have equals _required. Raise a TKUnitIntegerAssertionFailedError if the check fails.";

      assertInt(_required, _have, "");
   }

   protected method assertInt(int _required, int _have, String _msg) {
      explain "Check whether _have equals _required. Raise a TKUnitIntegerAssertionFailedError if the check fails.";

      if(_have != _required)
      {
         throw TKUnitIntegerAssertionFailedError(_msg+"("+_have+" != "+_required+")");
      }
   }

   protected method assertqFloat(float _required, float _have) {
      explain "Check whether _have equals _required. Raise a TKUnitFloatAssertionFailedError if the check fails.";

      assertFloat(_required, _have, "");
   }

   protected method assertFloat(float _required, float _have, String _msg) {
      explain "Check whether _have equals _required. Raise a TKUnitFloatAssertionFailedError if the check fails.";

      if(_have != _required)
      {
         throw TKUnitFloatAssertionFailedError(_msg+"("+_have+" != "+_required+")");
      }
   }

   protected method assertqFloatEpsilon(float _required, float _have, float _epsilon) {
      explain "Check whether _have roughly equals _required. Raise a TKUnitFloatAssertionFailedError if the check fails.";

      assertFloatEpsilon(_required, _have, _epsilon, "");
   }

   protected method assertFloatEpsilon(float _required, float _have, float _epsilon, String _msg) {
      explain "Check whether _have roughly equals _required. Raise a TKUnitFloatAssertionFailedError if the check fails.";

      if( ((_required-_epsilon) > _have) || ((_required+_epsilon) < _have) )
      {
         throw TKUnitFloatAssertionFailedError(_msg+"("+_have+" !=~ "+_required+")");
      }
   }

   protected method assertqObject(Object _required, Object _have) {
      assertObject(deref _required, deref _have, "");
   }

   protected method assertObject(Object _required, Object _have, String _msg) {
      explain "Check whether _have equals _required. Raise a TKUnitObjectAssertionFailedError if the check fails.";

      if(_required != null)
      {
         if(_have != null)
         {
            if(_have != _required)
            {
               throw TKUnitObjectAssertionFailedError(_msg+"("+#(_have)+" != "+#(_required)+")");
            }
         }
         else
         {
            throw TKUnitObjectAssertionFailedError(_msg+"have is null");
         }
      }
      else
      {
         if(_have != null)
         {
            throw TKUnitObjectAssertionFailedError(_msg+"have is not null");
         }
      }
   }

   protected method assertqType(var _required, var _have) {
      explain "Check whether _have and _require are of the same type. Raise a TKUnitTypeAssertionFailedError if the check fails.";

      assertType(_required, _have, "");
   }

   protected method assertType(var _required, var _have, String _msg) {
      explain "Check whether _have and _require are of the same type. Raise a TKUnitTypeAssertionFailedError if the check fails.";

      int idHave = typeid(_have);
      int idRequired = typeid(_required);
      if(idHave == YAC_TYPE_STRING)
      {
         idHave = YAC_TYPE_OBJECT;
      }
      if(idRequired == YAC_TYPE_STRING)
      {
         idRequired = YAC_TYPE_OBJECT;
      }
      if(idHave == idRequired)
      {
         if(idRequired >= YAC_TYPE_OBJECT)
         {
            if(_required != null)
            {
               if(_have != null)
               {
                  if(_have instanceof _required)
                  {
                     // Passed !
                     return; 
                  }
                  else
                  {
                     throw TKUnitTypeAssertionFailedError(_msg+"_have has different objecttype ("+typename(_have)+" != "+typename(_required)+")");
                  }
               }
               else
               {
                  throw TKUnitTypeAssertionFailedError(_msg+"_have is null");
               }
            }
            else
            {
               if(_have == null)
               {
                  // Passed !
                  return;
               }
               else
               {
                  throw TKUnitTypeAssertionFailedError(_msg+"_have is not null");
               }
            }
         }
         else
         {
            // Passed !
            return;
         }
      }
      else
      {
         throw TKUnitTypeAssertionFailedError(_msg+"type mismatch (_have="+typename(_have)+" _required="+typename(_required)+")");
      }
   }

}
