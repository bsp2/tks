
// test SysEx I/O

String DEV_NAME = "MONTAGE-1";

use tkmidi;

MIDIIn  midiin;
MIDIOut midiout;

// open input device
if(midiin.openByName(DEV_NAME))
{
   trace "ok, MIDI input device \""+midiin.deviceName+"\" opened (deviceIdx="+midiin.deviceIdx+")";
}
else
{
   trace "failed to open MIDI input device \""+midiin.deviceName+"\"";
   exit(10);
}

// open output device
if(midiout.openByName(DEV_NAME))
{
   trace "ok, MIDI output device \""+midiout.deviceName+"\" opened (deviceIdx="+midiout.deviceIdx+")";
}
else
{
   trace "failed to open MIDI output device \""+midiout.deviceName+"\"";
   exit(10);
}

Buffer ib; ib.size = 1024;
Buffer ob; ob.size = 1024;

midiin.start();

if(1)
{
   // SysEx: Send Yamaha Montage identity request
   trace "SysEx: send identity request";
   ob.offset = 0;
   ob.i8 = $F0;
   ob.i8 = $7E;
   ob.i8 = $00; // device id
   ob.i8 = $06;
   ob.i8 = $01;
   ob.i8 = $F7;
   midiout.sendBuffer(ob);

   // Wait for identity reply
   RecordedMIDIEvent recEv <= midiin.waitNextEvent(2000/*timeout*/);
   trace "SysEx: recv identity request recEv="+#(recEv);
   if(null != recEv)
   {
      if(recEv.isLongMessage())
      {
         // expected sz=13 data=7E 7F 06 02 43 00 41 dd dd mm 00 00 7F
         trace "SysEx Yamaha Montage identity reply sz="+recEv.size+":";
         ib.offset = 0;
         recEv.copyToStream(ib);
         ib.hexdump(0, ib.offset);
      }
   }
}
