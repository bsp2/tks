// ----
// ---- file: class_static.tks
// ---- date: 28-Mar-2005
// ---- 
// ---- info: tests static class members and methods
//

class MyBaseClass {
   static protected int pro_k=128; // declare integer which is visible in all derived classes
   static public    int pub_k=256; // declare integer which is visible everywhere
   static String s="hello, world."; // declare String which is visible everywhere
   static int ia[]<=[1,2,3]; // declare IntArray and assign pointer to constant [1,2,3] array; visible everywhere
   static int ia2[]=[4,5,6]; // declare IntArray and copy content of constant [4,5,6] array; visible everywhere
   static Float f=PI; // declare Float object and assign float value PI to it; visible everywhere
   static Double d=PI*0.000001; // declare Double object and assign float value PI*... to it; visible everywhere
   static Value v<=#({#["a"=[1,2,3]]}); // declare Value object and assign list consisting of one HashTable node, which stores the IntArray [1,2,3] using the key "a"; visible everywhere
   static String snull<=null; // declare String object and assign null pointer (0) to it; visible everywhere
   static HashTable ht<=#["a"=1, "b"=2, "c"=3]; // declare HashTable and assign reference to constant HashTable object; visible everywhere
   static ListNode l<={1,2.3,"4","...",ht}; // declare ListNode object and assign reference to constant ListNode; visible everywhere
   static List ls=l; // declare List object and assign contents of static member "l"; visible everywhere
}

class MyClass extends MyBaseClass {
   int x=9;

   static private int pri_i=32;
   static public  int pub_i=MyBaseClass.pro_k>>1;
   static public  int pub_k2=pro_k;

   static module int i=1;
   define int j=23;

   static method test() {
      // ---- static methods are the same as functions within
      // ---- a class context. They work without an instance object.
      print "s="+s;
      //print snull;
      print "snull="+snull;
      print "ia="+ia;
      print "ia2="+ia2;
      print "f="+f;
      print "d="+d;
      print "ht="+ht;
      print "l="+l;
      print "ls="+ls;
      String s2;
      print "v="+(v.string);
      print ++i;
   }
}

MyClass.test();      // 2
print ++MyClass.i;   // 3
print --MyClass.i;   // 2
print MyClass.i--;   // 2
print MyClass.i++;   // 1
MyClass.i--;
MyClass.i++;
print MyClass.i;     // 2

//print MyClass.pri_i;     // (not visible)
print MyClass.pub_i;
//print MyBaseClass.pro_k; // (not visible)
//print MyClass.pro_k;     // (not visible)
print MyBaseClass.pub_k;
print MyClass.pub_k;


MyClass mc;
print mc.x;      // test member
print MyClass.j; // test constant
