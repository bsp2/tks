
use tksdl;
use tkopengl;

float frot=0;
int numframesrendered=0;

function onDraw() {
   float dt=FPS.precision;
   glClearColor(0,0,0.2,1);
   glClear(GL_COLOR_BUFFER_BIT|GL_DEPTH_BUFFER_BIT);
   if( !(++numframesrendered&127) )
      trace "FPS.real="+FPS.real;
   zglInitOrtho(-1,-1);
   glRotatef(frot, 0,0,1);
   glColor3f(0.9,0.9,0.9);
   glBegin(GL_QUADS);
   glVertex2f(-1,-1);
   glVertex2f( 1,-1);
   glVertex2f( 1, 1);
   glVertex2f(-1, 1);
   glEnd();
   frot+=dt;
   wrap frot 0 360;
}

function onMouse(int _x, int _y, int _cbs, int _nbs) {
   print "x="+_x+" y="+_y+" cbs="+_cbs+" nbs="+_nbs;
}

function onDropFiles(int _x, int _y, StringArray _fileNames) {
   trace "onDropFiles: p=("+_x+"; "+_y+") numFiles="+_fileNames.numElements;
   String *s;
   int i = 0;
   foreach s in _fileNames
   {
      trace "onDropFiles: file["+i+"]=\""+s+"\"";
      i++;
   }
}

function onKeyboard(Key _k) {
   switch(_k.pressed)
   {
      case VKEY_ESCAPE:
         SDL.exitEventLoop();
         break;
   }
}

function main() {
   Viewport.openWindow(640, 480);
   use callbacks;
   use onDropFiles for SDL.onDropFiles;
   FPS.tickInterval=1000.0/60;
   FPS.limit=30;
   trace "xxx entering eventloop";
   SDL.eventLoop();
}

