// demonstrates how to output a pitch-modulated sinus signal using the audio device
use tksdl;

float pitch;
float pitch_phase=0;
float sine_phase=0;

float base_note=69.0;
float total_sec=0;

int framenr=0;

function onAudioFrame() {
   float freq=MIDINoteToFrequency(base_note+12*(0.5+0.5*sin(pitch_phase)));
   total_sec+=(60.0/125.0)/96.0;
   if( ! ((++framenr)&127) )
	{
      print "time= "+int(total_sec/60.0)+"m:"+(int(total_sec)%60)+"s.";
      print "freq="+freq;
	}
   pitch= (freq/(44100.0*0.5));
   pitch_phase+=0.01;
   wrap pitch_phase 0 2PI;
}

function onAudioBeginBlock(FloatArray _fa) {
   AudioDevice.processTimeFrames();
   AudioDevice.finishBlock();
}

function onAudioRender(FloatArray _fa) {
   int i=0;
   float cval;
   compile loop(_fa.numElements>>1) {
      cval=sin(sine_phase);
      _fa[i++]=cval;
      _fa[i++]=cval;
      sine_phase+=pitch;
   }
   wrap sine_phase 0 2PI;
}

function main() {
   use callbacks;
   AudioDevice.openDSP(44100, 8192);
   AudioDevice.ppq=96;
   AudioDevice.bpm=125;
   AudioDevice.volume=1.0;
   AudioDevice.start();
   SDL.eventLoop();
   AudioDevice.stop();
}
