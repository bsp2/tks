///
/// file   : debug.tks
/// author : Bastian Spiegel <bastian.spiegel@web.de>
/// descr. : debug overlays, fps display etc..
/// license: provided "AS IS". no copyright, no liability. use as you want.
/// created: ??Sep2001
/// changed: 30Mar2003 <fli> : removed class, uses functions + bindkey now
///
///
module Debug;

function Init();
function Toggle();
function ToggleConsole();
function ToggleMouseGrab();
function ToggleFullscreen();
function Draw();

int stime=0;
int nframes=0;
int enabled=0;
int con_enabled=0;
int mouse_grab;
int concount=0;
Font font;

function Init() {
    font.loadPixelFont("fonts/arial14");
    FPS.reset();
    mouse_grab=0;
    concount=0;
    nframes=1;
    stime=SDL.ticks;
}

function OnKeyboard(Key _key) {
    if(_key.pressed) switch(_key.name) 
	{
	case "g": ToggleMouseGrab(); break;
	case "escape": SDL.exitEventLoop(); break;
	case "f": ToggleFullscreen(); break;
	case "d": Toggle(); break;
	case "lctrl-d": ToggleConsole(); break;
	}
}

function Toggle() { 
    enabled=1-enabled; 
    trace "DrFPS::Toggle: enabled="+enabled;
}

function ToggleConsole() {
    con_enabled=1-con_enabled;
    trace "DrFPS::ToggleConsole: enabled="+con_enabled;
}

function ToggleMouseGrab() {
    mouse_grab=1-mouse_grab;
    if(mouse_grab) Mouse.grab();
    else Mouse.ungrab();
    trace "DrFPS::ToggleMouseGrab: enabled="+mouse_grab;
}

function ToggleFullscreen() {
    trace("DrFPS::ToggleFullScreen");
    Viewport.toggleFullScreen();
}

function Draw() {
    if(enabled)
	{
	    zglInit2D(Viewport.width, Viewport.height);
	    ////glDisable(GL_LIGHTING);
	    glEnable(GL_TEXTURE_2D);
  	    glEnable(GL_BLEND);
	    glDisable(GL_CULL_FACE);
	    glDisable(GL_DEPTH_TEST);
	    ////glDisable(GL_ALPHA_TEST);
	    glBlendFunc(GL_SRC_ALPHA, GL_ONE);
	    glColor4f(0,1,0,1);
	    glLoadIdentity();
	    font.drawi(0, 0, 
		       "----curFPS="+FPS.current+
		       "\nFPS="+FPS.real+
		       "\navgFPS="+FPS.average+
		       "\nmx="+Mouse.x+" my="+Mouse.y+
		       "\ndx="+Mouse.dx+" dy="+Mouse.dy);
	    glDisable(GL_BLEND);
	    glDisable(GL_TEXTURE_2D);
	}

    if(con_enabled)
	    if(++concount>200)
		{
		    trace 
			"----curFPS="+FPS.current+
			"\nFPS="+FPS.real+
			"\navgFPS="+FPS.average+
			"\nmx="+Mouse.x+" my="+Mouse.y+
			"\ndx="+Mouse.dx+" dy="+Mouse.dy;
		    concount=0;
		}
}
