
class CChild 
{
   tag Float  f;
   tag Double d;

   CChild() {
      f=PI;
      d=PI*0.000012345;
   }
}

class CParent
{
   tag int    i=42;
   tag float  f=PI;
   tag String s="hello, world.";
   tag float  fa[]=[1.0,2,3,4,5,6,7,8,9];
   tag CChild cchild;

   public method printDebug() {
      print "CParent.i       ="+i;
      print "CParent.f       ="+f;
      print "CParent.fa      ="+fa;
      print "CParent.cchild.f="+cchild.f;
      print "CParent.cchild.d="+cchild.d;
   }
}

function serializeToFile() {
   local File ofs;
   local CParent cp_file_out;
   ofs.openLocal("out.bin", IOS_OUT);
   ofs<<cp_file_out;
}

function deserializeFromFile() {
   local File ifs;
   local CParent cp_file_in;
   ifs.openLocal("out.bin", IOS_IN);
   ifs.offset=0;
   cp_file_in<<ifs;
   cp_file_in.printDebug();
}


function serializeToBuffer(Buffer _b) {
   local CParent cp_buffer_out;
   _b.size=1024;
   _b<<cp_buffer_out;
}

function deserializeFromBuffer(Buffer _b) {
   local CParent cp_buffer_in;
   _b.offset=0;
   
   cp_buffer_in<<_b;
   cp_buffer_in.printDebug();
}

function main() {

   print "File:";
   serializeToFile();
   deserializeFromFile();
   
   print "Buffer:";
   Buffer b;
   serializeToBuffer(b);
   deserializeFromBuffer(b);
}
