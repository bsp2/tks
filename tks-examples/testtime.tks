// testtime.tks
//   - by Bastian Spiegel <flink@tkscript.de>
//
//     demonstrates usage of the Time class plus some tool function
//     that you can include in your applications.
//
//
module Main;

function getDateString(Time _t) {
   PointerArray mnames;
   PointerArray dnames;
   prepare {
      mnames<=[
         "Jan", "Feb", "Mar", "Apr", "May", "Jun", 
         "Jul", "Aug", "Sep", "Oct", "Nov", "Dec"];
   }
   prepare {
      dnames<=["Sun", "Mon", "Tue", "Wed", "Thu", "Fri", "Sat"];
   }
   return _t.monthday+"-"+mnames[_t.month]+"-"+_t.year;
}

function getCurrentDateString() {
   Time t;
   t.now();
   return getDateString(t);
}

function getTimeString(Time _t) {
    Integer io_h;
    Integer io_m;
    Integer io_s;
    io_h.value=_t.hour;
    io_m.value=_t.min;
    io_s.value=_t.sec;
    return io_h.printf("%02i")+":"+io_m.printf("%02i")+":"+io_s.printf("%02i");
}


function getCurrentTimeString() {
    Time t;
    t.now();
    return getTimeString(t);
}

// 2005-12-31-23-59-59-123456789 format; i.e. year-month-monthday-hour-min-sec-nanoSec
function getRIOTimeString() {
   Time t;
   t.now();
   t.gmtime();
   Integer ioy = t.year, iom = t.month + 1, iomd = t.monthday, ioh = t.hour, iomin = t.min, iosec = t.sec, ionano = t.nanoSec;
   return 
      ioy   .printf("%04i")  + 
      iom   .printf("-%02i") + 
      iomd  .printf("-%02i") + 
      ioh   .printf("-%02i") + 
      iomin .printf("-%02i") +
      iosec .printf("-%02i") +
      ionano.printf("-%09i") ;
}

function main() {
    trace("\nit's already "+getCurrentTimeString()+" and still the "+getCurrentDateString()+".\n\n");

    Time t;
    t.now();
    PointerArray mnames<=[
			  "January", "February", "March", "April", "May", "June", 
			  "July", "August", "Sep", "October", "November", "December"];
    PointerArray dnames<=["Sunday", "Monday", "Tuesday", "Wednesday", "Thursday", "Friday", "Saturday"];
    trace("today is "+dnames[t.weekday]+", the "+t.monthday+". of "+mnames[t.month]+" in the year "+t.year+".");

    t.monthday=25;
    t.month=11;
    t.calc();
    trace("xmas is on a "+dnames[t.weekday]+", the "+t.yearday+". day of this year.");
    
    t.monthday=1;
    t.month=0;
    t.year=1970;
    t.calc();
    print "the "+getDateString(t)+" was a "+dnames[t.weekday]+".";  // => was a Thursday

    t.now();
    print "\nunix time (seconds since 1.Jan.1970): " + t.utime;

    print "\nRIO.timeString = " + getRIOTimeString();
    for(int x=0; x<100000; x++) ;
    print "RIO.timeString = " + getRIOTimeString();

    String t1=getRIOTimeString();
    compile for(int y=0; y<100000; y++) ;
    String t2=getRIOTimeString();
    print "RIO.timeString.t1 = " + t1;
    print "RIO.timeString.t2 = " + t2;
}
