// 02Feb2023

// % cat stdin_tablify_input.txt|tks stdin_tablify 14
// |:               |:               |:               |:               |
// |----------------|----------------|----------------|----------------|
// | some           | random         | text           |                |
// | to             | test           | tablification  |                |
// | this           | is             | another        | line           |
// +----------------+----------------+----------------+----------------+

int minW = 0;

if(Arguments.numElements > 0)
   minW = int(Arguments[0]);

class Utils {
   public static AlignString(String _s, int _numCols, String _sRep) : String {
      local String r = _s;
      int num = _s.numChars;
      if(num < _numCols)
      {
         r.appendRepeat(_sRep, (_numCols - num));
      }
      return deref r;
   }
}

Stream ifs <= StdInStream;
StringArray lines;
int lineNr = 1;
String line;
while(!ifs.eof())
{
   StdInStream.readLine(line, 4095);
   // trace "line["+lineNr+"]=\""+line+"\"";
   lineNr++;
   if(!line.isBlank())
      lines.add(line);
}

int numCells = 0;
StringArray words;
IntArray cellWidths;
int wIdx;
String *w;
foreach line in lines
{
   words <= line.splitSpace(true);
   numCells = mathMaxi(numCells, words.numElements);
   cellWidths.realloc(numCells);
   wIdx = 0;
   foreach w in words
   {
      cellWidths[wIdx] = mathMaxi(cellWidths.get(wIdx), mathMaxi(w.numChars, minW));
      wIdx++;
   }
}

// trace cellWidths;

wIdx = 0;
while(wIdx < numCells)
{
   if(wIdx > 0)
      stdout " ";
   stdout "|:";
   stdout Utils.AlignString("", cellWidths[wIdx++], " ");
}
stdout " |";
stdout "\n";

wIdx = 0;
while(wIdx < numCells)
{
   if(wIdx > 0)
      stdout "-";
   stdout "|-";
   stdout Utils.AlignString("", cellWidths[wIdx++], "-");
}
stdout "-|";
stdout "\n";

foreach line in lines
{
   words <= line.splitSpace(true);
   wIdx = 0;
   foreach w in words
   {
      if(wIdx > 0)
         stdout " ";
      stdout "| ";
      stdout Utils.AlignString(w, cellWidths[wIdx++], " ");
   }
   while(wIdx < numCells)
   {
      if(wIdx > 0)
         stdout " ";
      stdout "| ";
      stdout Utils.AlignString("", cellWidths[wIdx++], " ");
   }
   stdout " |";
   stdout "\n";
}

wIdx = 0;
while(wIdx < numCells)
{
   if(wIdx > 0)
      stdout "-";
   stdout "+-";
   stdout Utils.AlignString("", cellWidths[wIdx++], "-");
}
stdout "-+";
stdout "\n";
