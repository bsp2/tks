// $Id: random-java.code,v 1.5 2004/11/22 07:08:58 bfulgham Exp $
// http://shootout.alioth.debian.org/
//
// Brent Fulgham:  Changed to use 32-bit integers (like the C
// version), based on a suggestion by Yonik Seeley.

// ---- ported to tkscript by Bastian Spiegel <bs@tkscript.de>
// ---- on 14-Apr-2005.
// ----
// ---- Results: N=900000
// ----   java1.5.0_02: ~70ms
// ----    tks0.9.0.58: ~1082ms
// ----      perl5.8.5: ~1500ms
// ---- $ tks random 900000
// ---- Program output:
// ---- RunTest: t(ms)=1082
// ---- 75.544410150891636
// ---- RunTest_Opt: t(ms)=70
// ---- 75.5444
// ----

class random {
   define int IM = 139968;
   define int IA = 3877;
   define int IC = 29573;
   
   static int last = 42;
   function gen_random(Double max) {
      last = (last * IA + IC) % IM;
      return (max * last) / IM;
   }

   function RunTest() {
      int N = Arguments.numElements?Arguments[0]:900000;
      
      Double max=100;
      int t=milliSeconds();
      loop(N-1)
         gen_random(max);
      t=milliSeconds()-t;
      print "RunTest: t(ms)="+t;
      print gen_random(max);
   }
   
   function RunTest_Opt() compile { // ~16 times faster than RunTest()
      int N = Arguments.numElements?Arguments[0]:900000;
      
      int t=milliSeconds();
      int last = 42;
      float ret;
      loop(N)
      {
         last = (last * IA + IC) % IM;
         ret=(100.0 * last) / IM;
      }
      t=milliSeconds()-t;
      print "RunTest_Opt: t(ms)="+t;
      print ret;
   }

}

random.RunTest();
random.RunTest_Opt();

