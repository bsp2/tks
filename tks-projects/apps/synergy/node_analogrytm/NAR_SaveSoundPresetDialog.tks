// ----
// ---- file   : NAR_SaveSoundPresetDialog.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2019-2023 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "syntracker" sequencer.
// ----
// ---- created: 26Oct2019
// ---- changed: 25Nov2023
// ----
// ----
// ----
module NAR_SaveSoundPresetDialog;

use namespace ui;
use namespace st2;


class NAR_SaveSoundPresetDialog extends ActionConsumer {
   TextInputDialog *d;
   NAR_EditKitDialog *parent_dlg;
   StringArray *preset_names;

   public method run(NAR_EditKitDialog _parentDlg, StringArray _presetNames) {
      parent_dlg <= _parentDlg;
      preset_names <= _presetNames;

      d <= TextInputDialog.New("Save sound preset",
                               "Please enter sound preset name:",
                               "\aSave",
                               "Cancel",
                               parent_dlg.last_preset_name,
                               32, 24,
                               this
                               );
      d.b_provide_edited = true;
      updatePresetNameBackground(parent_dlg.last_preset_name);
      d.showNearMouse(10,-20);
   }

   protected method updatePresetNameBackground(String _name) {
      if(preset_names.containsObject(_name))
         d.setBackgroundTint(#20ffff00);
      else
         d.setBackgroundTint(0);
   }

   public virtual consumeAction(Action _action) : boolean {
      if(_action instanceof StringAction)
      {
         StringAction sac <= _action;
         if(sac.getActionProvider() == d)
         {
            local String name = sac.getStringValue();
            switch(sac.getActionName())
            {
               case TextInputDialog.ACTION_TEXTINPUTDIALOGCLOSED:
                  d <= null; // Delete dialog
                  if(!name.isBlank())
                  {
                     parent_dlg.saveSoundPreset2(name);
                     return true;
                  }
                  break;

               case TextInputDialog.ACTION_TEXTINPUTDIALOGEDITED:
                  updatePresetNameBackground(name);
                  return true;
            }
         }
      }
      return true;
   }
}
