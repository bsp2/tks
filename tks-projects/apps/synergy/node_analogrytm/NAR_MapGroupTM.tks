// ----
// ---- file   : NAR_MapGroupTM.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2016 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "syntracker" midi sequencer.
// ----
// ---- created: 05Mar2016
// ---- changed: 06Mar2016, 13Apr2016
// ----
// ----
// ----

module MNAR_MapGroupTM;

use namespace ui;
use namespace st2;


class NAR_MapGroupTM : TableModel {

   define int COL_UID     = 0;
   define int COL_ENABLE  = 1;
   define int COL_GLOBAL  = 2;
   define int COL_NAME    = 3;
   define int COL_ENTRIES = 4;

   NodeAnalogRytmPattern *pat;
   NodeAnalogRytm *parent_node;

   protected CheckBox cb_active_render;
   protected CheckBox cb_active_edit;

   protected CheckBox cb_global_render;
   protected CheckBox cb_global_edit;


   public virtual getNumColumns() : int {
      return 5;
   }

   public virtual getColumnCaption(int _col) : String {
      return ["UID", "Enable", "Global", "Name", "Num"][_col];
   }

   public virtual getNumRows() : int {
      return (null != pat) ? pat.groups.numElements : 0;
   }

   public virtual getCellRenderer(int _col, int _row, int _visibleRow) : Layer {
      if(null != pat)
      {
         NAR_MapGroup mapgrp <= pat.groups.get(_row);

         if(null != mapgrp)
         {
            CheckBox *cb;

            if(COL_ENABLE == _col)
            {
               if(_row != cursor_index)
               {
                  cb <= cb_active_render;
               }
               else
               {
                  // Row is possibly being edited (at least it is selected..)
                  cb <= cb_active_edit;
               }
               cb.setSelected(mapgrp.b_enable);
               cb.setInnerPadTop(1.0f);
               cb.setPositionX(4.0f);
               return cb;
            }

            if(COL_GLOBAL == _col)
            {
               if(_row != cursor_index)
               {
                  cb <= cb_global_render;
               }
               else
               {
                  // Row is possibly being edited (at least it is selected..)
                  cb <= cb_global_edit;
               }
               cb.setSelected(mapgrp.b_global);
               cb.setInnerPadTop(1.0f);
               cb.setPositionX(4.0f);
               return cb;
            }
         }
      }

      if(COL_ENABLE == _col)
      {
         cb <= cb_active_render;
         cb.setInnerPadTop(1.0f);
         cb.setPositionX(4.0f);
         return cb;
      }

      if(COL_GLOBAL == _col)
      {
         cb <= cb_global_render;
         cb.setInnerPadTop(1.0f);
         cb.setPositionX(4.0f);
         return cb;
      }

      Label lb <= TableModel::getCellRenderer(_col, _row, _visibleRow);
      lb.setFontByName("big8");
      return lb;
   }

   public virtual getCellCaption(int _col, _row) : String {
      if(null != pat)
      {
         NAR_MapGroup mapgrp <= pat.groups.get(_row);

         switch(_col)
         {
            case COL_UID:
               if(null != mapgrp)
               {
                  return String(mapgrp.uid);
               }
               else
               {
                  return "?";
               }

            case COL_ENABLE:
               if(null != mapgrp)
               {
                  return String(mapgrp.b_enable);
               }
               else
               {
                  return "?";
               }

            case COL_GLOBAL:
               if(null != mapgrp)
               {
                  return String(mapgrp.b_global);
               }
               else
               {
                  return "?";
               }

            case COL_NAME:
               if(null != mapgrp)
               {
                  return mapgrp.name;
               }
               else
               {
                  return "-";
               }

            case COL_ENTRIES:
               if(null != mapgrp)
               {
                  return mapgrp.entries.numElements;
               }
               else
               {
                  return "-";
               }
         }
      }
   }

   public virtual initTableModel() {
      initCheckBox(cb_active_render);
      initCheckBox(cb_active_edit);

      initCheckBox(cb_global_render);
      initCheckBox(cb_global_edit);

      TableModel::initTableModel();
   }

   public virtual getMinimumColumnWidth(int _col) : int {
      switch(_col)
      {
         case COL_UID:
            return 24;

         case COL_ENABLE:
            return 40;

         case COL_GLOBAL:
            return 40;

         case COL_NAME:
            return 85;

         case COL_ENTRIES:
            return 32;
      }
   }

   public virtual getPreferredColumnWidth(int _col) : int {
      switch(_col)
      {
         case COL_UID:
            return 24;

         case COL_ENABLE:
            return 40;

         case COL_GLOBAL:
            return 40;

         case COL_NAME:
            return 640;

         case COL_ENTRIES:
            return 32;
      }
   }

   public virtual handleCellOnMouse(int _col, int _row, MouseEvent _ev) : boolean {
      boolean bChanged;
      boolean bOldState;

      if(null != pat)
      {
         NAR_MapGroup mapgrp <= pat.groups.get(_row);

         if(null != mapgrp)
         {

            if(COL_ENABLE == _col)
            {
               // trace "xxx handleCellOnMouse: 1"+" _ev.current_state="+_ev.current_state+" _ev.changed_state="+_ev.changed_state;
               bOldState = cb_active_edit.isSelected();

               if(cb_active_edit.onMouse(_ev))
               {
                  bChanged = cb_active_edit.isSelected();

                  if(bOldState ^ bChanged)
                  {
                     mapgrp.setEnable(cb_active_edit.isSelected());

                     Global.Print((mapgrp.b_enable?"Enable":"Disable")+" Map Group \""+mapgrp.name+"\"");
                  }

                  return true;
               }
               // ...
            }

            if(COL_GLOBAL == _col)
            {
               // trace "xxx handleCellOnMouse: 1"+" _ev.current_state="+_ev.current_state+" _ev.changed_state="+_ev.changed_state;
               bOldState = cb_global_edit.isSelected();

               if(cb_global_edit.onMouse(_ev))
               {
                  bChanged = cb_global_edit.isSelected();

                  if(bOldState ^ bChanged)
                  {
                     mapgrp.setGlobal(cb_global_edit.isSelected());

                     Global.Print((mapgrp.b_global?"Global":"Local")+" Map Group \""+mapgrp.name+"\"");
                  }

                  return true;
               }
               // ...
            }
         }
      }

      return false;
   }
}
