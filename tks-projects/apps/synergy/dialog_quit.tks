// ----
// ---- file   : dialog_quit.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2006-2017 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "syntracker" sequencer.
// ----
// ---- changed: 14Sep2006, ..., 28Feb2011, 25May2013, 11Aug2014, 18Aug2014, 31Aug2014, 04Jun2017
// ----
// ----
// ----
module MDialog_Quit;

use namespace ui;


class Dialog_Quit extends ActionConsumer {
   ChoiceDialog *d;

   public method run() {
      d <= ChoiceDialogMMT.New("Quit",
                               "Exit (without saving) ?",
                               "Discard changes and quit", "Don't quit", null,
                               "choice",
                               this);
      d.setDefaultSelection(ChoiceDialog.SELECT_NO);
      d.setMessageFont("big6");
      d.setButtonIcons(UI.GetIcon("exit"), UI.GetIcon("cancel"), null);
      d.setButtonFont("big5");
      d.invalidateSizeCaches();
      d.resizeToMinimum();
      d.showCentered();
   }

   public virtual consumeAction(IntAction _action) returns boolean {
      if(_action.getActionProvider() == d)
      {
         d <= null; // Delete dialog
         Global.Debug("Dialog_Quit: _action.getIntValue()="+_action.getIntValue());
         switch(_action.getIntValue())
         {
            case ChoiceDialog.SELECT_YES:
               Global.cmd_file_quit_2();
               return true;

            case ChoiceDialog.SELECT_NO:
               Global.Debug2("CMD file_quit aborted.");
               return true;
         }
      }
      return false;
   }
}
