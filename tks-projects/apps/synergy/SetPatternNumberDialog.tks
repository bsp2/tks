// ----
// ---- file   : SetPatternNumberDialog.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2006-2017 by Bastian Spiegel. 
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See 
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "syntracker" sequencer.
// ----
// ---- changed: 14Sep2006, ..., 28Feb2011, 16Jan2012, 17Jan2012, 27May2013, 26Jan2015
// ----          11Feb2015, 10Mar2017, 13Oct2017, 30Oct2017
// ----
// ----
// ----
module MSetPatternNumberDialog;

use namespace ui;
use namespace st2;


class SetPatternNumberDialog extends ActionConsumer {
   TextInputDialog *d;
   Layer *old_kbdfocus_layer;

   int last_fmt = 2;  // 0=dec, 1=hex, 2=quad

   public method run(int _patNr) {

      old_kbdfocus_layer <= UI.keyboard_layer;

      if(null == d)
      {
         d <= TextInputDialog.New("Enter pattern number", 
                                  "Please enter pattern number (0..127 / $00..$7f / 1A..32D):",
                                  "\aOk",
                                  "Cancel",
                                  "?",
                                  5, 5,
                                  this
                                  );
      }
      String patStr;
      switch(last_fmt)
      {
         default:
         case 0:
            patStr = _patNr;
            break;
         case 1:
            Integer io = _patNr;
            patStr = io.printf("$%02x");
            break;
         case 2:
            patStr = (_patNr / 4) + 1;
            patStr.append(["A", "B", "C", "D"].get(_patNr & 3));
            break;
      }

      d.setDefaultText(patStr);
      d.showCentered();
   }
    
   public virtual consumeAction(Action _action) : boolean {
      StringAction *sac;

      if(_action.getActionProvider() == d)
      {
         String acName <= _action.getActionName();
         if(TextInputDialog.ACTION_TEXTINPUTDIALOGCANCELED == acName)
         {
            UI.SetKeyboardFocus(old_kbdfocus_layer);
            return true;
         }
         if("" != acName)
         {
            if(_action instanceof StringAction)
            {
               sac <= _action;
               if("" != sac.getStringValue())
               {
                  String patStr <= sac.getStringValue();
                  patStr.toLower();

                  int patNr;

                  if(patStr <= "t")
                  {
                     patNr = Node.TMP_PAT_NR;
                  }
                  else if(patStr <= "s")
                  {
                     patNr = Node.SCRATCH_PAT_NR;
                  }
                  else if(patStr.endsWith("a"))
                  {
                     patStr.replace("a", "");
                     patNr = ((int(patStr)-1)*4);
                     last_fmt = 2;
                  }
                  else if(patStr.endsWith("b"))
                  {
                     patStr.replace("b", "");
                     patNr = ((int(patStr)-1)*4) + 1;
                     last_fmt = 2;
                  }
                  else if(patStr.endsWith("c"))
                  {
                     patStr.replace("c", "");
                     patNr = ((int(patStr)-1)*4) + 2;
                     last_fmt = 2;
                  }
                  else if(patStr.endsWith("d"))
                  {
                     patStr.replace("d", "");
                     patNr = ((int(patStr)-1)*4) + 3;
                     last_fmt = 2;
                  }
                  else
                  {
                     // Dec or Hex
                     patNr = patStr;
                     last_fmt = (
                        (-1 != patStr.indexOfChar('$', 0)) ||
                        (-1 != patStr.indexOfChar('#', 0)) ||
                        (-1 != patStr.indexOf("0x", 0))
                                 ) ? 1: 0;
                  }

                  if(patNr < 0)
                  {
                     patNr = 0;
                  }
                  else if(patNr > 127)
                  {
                     patNr = 127;
                  }

                  root_form.pg_node.showPatternNumberDialog2(patNr);
                  UI.SetKeyboardFocus(old_kbdfocus_layer);
               } // "" != acValue
            } // if is StringAction
         } // "" != acName
         return true;
      }
      return false;
   }
}
