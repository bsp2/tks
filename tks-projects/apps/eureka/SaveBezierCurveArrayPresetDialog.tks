// ----
// ---- file   : SaveBezierCurveArrayPresetDialog.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2017-2025 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "syntracker" sequencer.
// ----
// ---- created: 01Dec2017
// ---- changed: 01Dec2017, 29Dec2018, 20Aug2020, 23Dec2023, 21Jun2025
// ----
// ----
// ----
module MSaveBezierCurveArrayPresetDialog;

use namespace ui;
use namespace st2;


class SaveBezierCurveArrayPresetDialog extends ActionConsumer {
   TextInputDialog *d;
   BezierCurveArrayForm *parent_form;

   public method run(BezierCurveArrayForm _parentForm) {
      parent_form <= _parentForm;

      d <= TextInputDialog.New("Save curve preset",
                               "Please enter curve preset name:",
                               "\aSave",
                               "Cancel",
                               parent_form.last_preset_name,
                               32, 24,
                               this
                               );
      d.setEnableProvideEdited(true);
      d.showNearMouse(10,-20);
      updatePresetNameBackground(parent_form.last_preset_name);
   }

   protected method updatePresetNameBackground(String _name) {
      d.setBackgroundTint(0);
      if(!_name.isBlank())
      {
         local String pathName = Utils.ToNativePathName(STConfig.curve_rootpath+"/"+_name+".curve");
         if(Utils.CanFileBeRead(pathName))
            d.setBackgroundTint(#20ffff00);
      }
   }

   public virtual consumeAction(Action _action) : boolean {
      if(_action instanceof StringAction)
      {
         StringAction sac <= _action;
         if(sac.getActionProvider() == d)
         {
            local String acName <= sac.getActionName();
            local String name = sac.getStringValue();
            if(TextInputDialog.ACTION_TEXTINPUTDIALOGEDITED == acName)
            {
               // char typed
               updatePresetNameBackground(name);
               return true;
            }
            else if(TextInputDialog.ACTION_TEXTINPUTDIALOGCLOSED == acName)
            {
               d <= null; // Delete dialog
               if(!name.isBlank())
               {
                  parent_form.saveCurvePreset2(name);
               }
               parent_form.refocusDefault();
            }
         }
      }
      return true;
   }
}
