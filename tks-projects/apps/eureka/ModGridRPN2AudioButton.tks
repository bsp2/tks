// ----
// ---- file   : ModGridRPN2AudioButton.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2021-2023 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "syntracker" midi/audio sequencer.
// ----
// ---- created: 13Apr2021
// ---- changed: 04Dec2021, 27Dec2021, 04Oct2023, 01Nov2023
// ----
// ----
// ----

module MModGridRPN2AudioButton;

use namespace ui;
use namespace st2;


// <class.png>
class ModGridRPN2AudioButton : ModGridButton, ActionConsumer {
   protected ModRPN2Audio *mod;

   protected ComboBox   *cm_rpn;
   protected FloatParam *fp_rise;
   protected FloatParam *fp_fall;
   protected FloatParam *fp_curve;


   // <ui_init.png>
   public method initRPN2AudioButton(ModRPN2Audio _mod) {

      initModGridButton();

      setLayout(SuperBorderLayout);
      setEnableKbdFocusOnMouse(false);
      setLookAndFeel(LookAndFeel.LIGHT);

      mod <= _mod;

      cm_rpn <= ComboBox.New();
      cm_rpn.setAlignment(Layout.CENTERY);
      cm_rpn.setRequiredSizeY(ModGrid.GetModButtonSizeY());
      cm_rpn.setLookAndFeel(LookAndFeel.LIGHT);
      local StringArray optRPN;
      int rpnNr = 1;
      loop(16)
         optRPN.add(String(rpnNr++));
      cm_rpn.setOptions(optRPN);
      cm_rpn.setSelectedOption(mod.rpn_offset);
      cm_rpn.setToolTipCaption("RPN\n\n (note) 1..16 => RPN #1..#16");
      cm_rpn.setPadLeft(2);
      addLayer(deref cm_rpn, Layout.LEFT);

      fp_rise <= FloatParam.New();
      fp_rise.setVisibleTextLength(2);
      fp_rise.setStep(1.0);
      fp_rise.setMinMaxValues(0.0, 100.0);
      fp_rise.setResetValue(50.0f);
      fp_rise.setValue(mod.slew_rise * 100.0);
      fp_rise.setPrecision(3);
      fp_rise.setMousePrecision(0);
      fp_rise.setAlignment(Layout.CENTERY);
      fp_rise.setToolTipCaption("Rise Slew Amount");
      if(UI.IsLoDPI())
         fp_rise.setPadBottom(1);
      addLayer(deref fp_rise, Layout.RIGHT);

      fp_fall <= FloatParam.New();
      fp_fall.setVisibleTextLength(2);
      fp_fall.setStep(1.0);
      fp_fall.setMinMaxValues(0.0, 100.0);
      fp_fall.setResetValue(80.0f);
      fp_fall.setValue(mod.slew_fall * 100.0);
      fp_fall.setPrecision(3);
      fp_fall.setMousePrecision(0);
      fp_fall.setAlignment(Layout.CENTERY);
      fp_fall.setToolTipCaption("Fall Slew Amount");
      if(UI.IsLoDPI())
         fp_fall.setPadBottom(1);
      addLayer(deref fp_fall, Layout.RIGHT);

      fp_curve <= FloatParam.New();
      fp_curve.setVisibleTextLength(2);
      fp_curve.setStep(0.1);
      fp_curve.setMinMaxValues(-9.0, 9.0);
      fp_curve.setResetValue(0.0f);
      fp_curve.setValue(mod.curve * 9.0);
      fp_curve.setPrecision(4);
      fp_curve.setMousePrecision(1);
      fp_curve.setAlignment(Layout.CENTERY);
      fp_curve.setPadRight(2.0f);
      fp_curve.setToolTipCaption("Log..Lin..Exp Curve");
      if(UI.IsLoDPI())
         fp_curve.setPadBottom(1);
      addLayer(deref fp_curve, Layout.RIGHT);
   }

   // <method_set.png>
   public virtual setEnableHold(boolean _bHold) {
      // prevent Button HOLD_CLICK action (=> edit modmatrix)
   }

   // <ui_render.png>
   public virtual onDraw() {
      UIRenderer.DrawDefaultControlBackground(0, 0, getSizeX(), getSizeY());
      drawGridBorder();
      UIRenderer.DrawDefaultRaisedBorderRight(0, 0, getSizeX(), getSizeY());
   }

   // <ui_consume.png>
   public virtual consumeAction(Action _ac) : boolean {
      String acName <= _ac.getActionName();
      ActionProvider ap <= _ac.getActionProvider();

      switch(@(ap))
      {
         case @(cm_rpn):
            mod.setRPNOffset(cm_rpn.getSelectedOption());
            Global.Print("RPN# is "+cm_rpn.getSelectedOptionName());
            current_project.markAsModified();
            return true;

         case @(fp_rise):
            mod.setSlewRise(fp_rise.getValue() / 100.0);
            mod.addToUIParamHistory(ModRPN2Audio.PARAM_SLEW_RISE);
            Global.Print("Rise slew rate is "+fp_rise.getValue());
            current_project.markAsModified();
            return true;

         case @(fp_fall):
            mod.setSlewFall(fp_fall.getValue() / 100.0);
            mod.addToUIParamHistory(ModRPN2Audio.PARAM_SLEW_FALL);
            Global.Print("Fall slew rate is "+fp_fall.getValue());
            current_project.markAsModified();
            return true;

         case @(fp_curve):
            mod.setCurve(fp_curve.getValue() / 9.0);
            mod.addToUIParamHistory(ModRPN2Audio.PARAM_CURVE);
            Global.Print("Curve is "+fp_curve.getValue());
            current_project.markAsModified();
            return true;
      }

      return ModGridButton::consumeAction(_ac);
   }

}
