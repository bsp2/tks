// ----
// ---- file   : CycleSaveMachinePresetDialog.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2020-2024 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "syntracker" midi/audio sequencer.
// ----
// ---- created: 18Aug2020
// ---- changed: 02Dec2023, 03Oct2024
// ----
// ----
// ----
module MCycleSaveMachinePresetDialog;

use namespace ui;
use namespace st2;


// <class.png>
class CycleSaveMachinePresetDialog extends ActionConsumer {
   TextInputDialog *d;
   CycleSynthDialog *parent_dlg;

   public method run(CycleSynthDialog _parentDlg) {
      parent_dlg <= _parentDlg;

      d <= TextInputDialog.New("Save machine preset",
                               "Please enter machine preset name:",
                               "\aSave",
                               "Cancel",
                               parent_dlg.last_preset_name,
                               32, 24,
                               this
                               );
      d.setEnableProvideEdited(true);
      d.showNearMouse(10*UI.font_scaling, -20*UI.font_scaling);
      updatePresetNameBackground(parent_dlg.last_preset_name);
   }

   protected method updatePresetNameBackground(String _name) {
      d.setBackgroundTint(0);
      if(!_name.isBlank())
      {
         local String pathName <= Utils.ToNativePathName(STConfig.cycle_machine_preset_path+"/"+Utils.ConvertToFileName(_name.trim())+".cy");
         if(Utils.CanFileBeRead(pathName))
            d.setBackgroundTint(#20ffff00);
      }
   }

   public virtual consumeAction(Action _ac) : boolean {
      local String acName <= _ac.getActionName();
      if(_ac instanceof StringAction)
      {
         StringAction sac <= _ac;
         String name = sac.getStringValue();
         if(sac.getActionProvider() == d)
         {
            if(TextInputDialog.ACTION_TEXTINPUTDIALOGEDITED == acName)
            {
               // char typed
               updatePresetNameBackground(name);
               return true;
            }
            d <= null; // Delete dialog
            if(TextInputDialog.ACTION_TEXTINPUTDIALOGCLOSED == acName)
            {
               if(!name.isBlank())
               {
                  parent_dlg.saveMachinePreset2(name);
                  return true;
               }
            }
         }
      }

      if(TextInputDialog.ACTION_TEXTINPUTDIALOGCANCELED == acName)
      {
         UI.SetKeyboardFocus(parent_dlg);
      }

      return true;
   }
}
