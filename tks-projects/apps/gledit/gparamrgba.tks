// ----
// ---- file   : gparamrgba.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2020 by Bastian Spiegel.
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "GL edit" application.
// ----
// ---- created: 13Jun2020
// ---- changed: 14Jun2020, 15Jun2020, 20Jun2020, 27Jun2020
// ----
// ----
// ----
module MGParamRGBA;

use namespace ui;
use namespace gledit;


// <class.png>
class GParamRGBA : GParam {
   int value;  // #aarrggbb
   int def_value;

   // <method_new.png>
   static New(String _id, String _caption, int _def) {
      local GParamRGBA p;
      p.init(_id, _caption, _def);
      return deref p;
   }

   // <method_init.png>
   public method init(String _id, String _caption, int _def) {
      initParam(_id, _caption);
      def_value = _def;
      value = def_value;
   }

   // <method_set.png>
   public method setValue(int _val) {
      value = _val;
   }

   // <method_get.png>
   public method getRAsFloat() : float {
      return ((value >> 16) & 255) * (1.0f / 255.0);
   }

   // <method_get.png>
   public method getGAsFloat() : float {
      return ((value >>  8) & 255) * (1.0f / 255.0);
   }

   // <method_get.png>
   public method getBAsFloat() : float {
      return ((value      ) & 255) * (1.0f / 255.0);
   }

   // <method_get.png>
   public method getAAsFloat() : float {
      return ((value >> 24) & 255) * (1.0f / 255.0);
   }

   // <method_get.png>
   public virtual getString() : String {
      local Integer io = value;
      return io.printf("#%06x");
   }

   // <method_set.png>
   public virtual copyParamValueFrom(GParam _o) {
      if(_o instanceof GParamRGBA)
      {
         GParamRGBA o <= _o;
         value = o.value;
      }
   }

   // <save.png>
   public virtual saveStateParam(local Stream ofs) {
      GParam::saveStateParam(ofs);

      // Version
      ofs.i16 = 1;

      // Value
      ofs.i32 = value;
   }

   // <load.png>
   public virtual loadStateParam(local Stream ifs) : boolean {
      // (note) base class param info is loaded by LoadStateParam

      // Version
      short ver = ifs.u16;

      if(1 <= ver <= 1)
      {
         // Value
         value = ifs.i32;

         // Succeeded
         return true;
      }
      else
      {
         trace "[---] GParamRGBA::loadStateParam: invalid version "+ver;
      }
      return false;
   }
}
