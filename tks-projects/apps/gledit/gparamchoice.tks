// ----
// ---- file   : gparamchoice.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2020 by Bastian Spiegel. 
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See 
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "GL edit" application.
// ----
// ---- created: 27Jun2020
// ---- changed: 09Jul2020
// ----
// ----
// ----
module MGParamChoice;

use namespace ui;
use namespace gledit;


// <class.png>
class GParamChoice : GParam {
   int value;
   int def_value;
   StringArray options;


   // <method_new.png>
   static New(String _id, String _caption, int _def, StringArray _options) {
      local GParamChoice p;
      p.init(_id, _caption, _def, deref _options);
      return deref p;
   }

   // <method_init.png>
   public method init(String _id, String _caption, int _def, StringArray _options) {
      initParam(_id, _caption);
      def_value = _def;
      value = def_value;
      options = deref _options;
   }

   // <method_set.png>
   public method setValue(int _val) {
      value = _val;
   }

   // <method_set.png>
   public method getIOType(int _val) {
      value = _val;
   }

   // <method_get.png>
   public virtual getString() : String {
      return options.get(value);
   }

   // <method_set.png>
   public virtual copyParamValueFrom(GParam _o) {
      if(_o instanceof GParamChoice)
      {
         GParamChoice o <= _o;
         value = o.value;
      }
   }

   // <save.png>
   public virtual saveStateParam(local Stream ofs) {
      GParam::saveStateParam(ofs);

      // Version
      ofs.i16 = 1;

      // Value
      ofs.i32 = value;
   }

   // <load.png>
   public virtual loadStateParam(local Stream ifs) : boolean {
      // (note) base class param info is loaded by LoadStateParam

      // Version
      short ver = ifs.u16;

      if(1 <= ver <= 1)
      {
         // Value
         value = ifs.i32;

         // Succeeded
         return true;
      }
      else
      {
         trace "[---] GParamChoice::loadStateParam: invalid version "+ver;
      }
      return false;
   }

}
