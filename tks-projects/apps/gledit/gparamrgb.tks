// ----
// ---- file   : gparamrgb.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2020 by Bastian Spiegel. 
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See 
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "GL edit" application.
// ----
// ---- created: 13Jun2020
// ---- changed: 14Jun2020, 15Jun2020, 20Jun2020
// ----
// ----
// ----
module MGParamRGB;

use namespace ui;
use namespace gledit;


// <class.png>
class GParamRGB : GParam {
   int value;  // #00rrggbb
   int def_value;

   // <method_new.png>
   static New(String _id, String _caption, int _def) {
      local GParamRGB p;
      p.init(_id, _caption, _def);
      return deref p;
   }

   // <method_init.png>
   public method init(String _id, String _caption, int _def) {
      initParam(_id, _caption);
      def_value = _def;
      value = def_value;
   }

   // <method_set.png>
   public method setValue(int _val) {
      value = _val & 0x00FFffFF;
   }

   // <method_get.png>
   public virtual getString() : String {
      local Integer io = value;
      return io.printf("#%08x");
   }

   // <method_set.png>
   public virtual copyParamValueFrom(GParam _o) {
      if(_o instanceof GParamRGB)
      {
         GParamRGB o <= _o;
         value = o.value;
      }
   }

   // <save.png>
   public virtual saveStateParam(local Stream ofs) {
      GParam::saveStateParam(ofs);

      // Version
      ofs.i16 = 1;

      // Value
      ofs.i32 = value;
   }

   // <load.png>
   public virtual loadStateParam(local Stream ifs) : boolean {
      // (note) base class param info is loaded by LoadStateParam

      // Version
      short ver = ifs.u16;

      if(1 <= ver <= 1)
      {
         // Value
         value = ifs.i32;

         // Succeeded
         return true;
      }
      else
      {
         trace "[---] GParamRGB::loadStateParam: invalid version "+ver;
      }
      return false;
   }

}
