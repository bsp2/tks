// ----
// ---- file   : gconstantfloat.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : (c) 2020 by Bastian Spiegel. 
// ----          Distributed under terms of the GNU LESSER GENERAL PUBLIC LICENSE (LGPL). See 
// ----          http://www.gnu.org/licenses/licenses.html#LGPL or COPYING for further information.
// ----
// ---- info   : This is part of the "GL edit" application.
// ----
// ---- created: 12Jun2020
// ---- changed: 13Jun2020, 14Jun2020, 15Jun2020, 16Jun2020, 18Jun2020, 20Jun2020, 09Jul2020
// ----          20Jul2020, 08Aug2020, 14Aug2020
// ----
// ----
// ----
module MGConstantFloat;

use namespace ui;
use namespace gledit;


// <class.png>
class GConstantFloat : GConstant {

   // <method_new.png>
   public static New() : GConstantFloat {
      local GConstantFloat r;
      r.init();
      return deref r;
   }

   // <method_init.png>
   public virtual init() {
      // trace "xxx GConstantFloat::init";
      GConstant::init();
      type = TYPE_FLOAT;

      addParamFloat("value", "value", 0.0f, -10.0f, FLT_MIN, 10.0f, FLT_MAX, 0.0f);

      addInput("value", TYPE_FLOAT);

      addOutput(GOutput.OUT_DEF, type);
   }

   // <method_get.png>
   public virtual getString() : String {
      float value = getFloatParamById("value");
      return String(value);
   }

   // <method.png>
   public virtual emit(local String       _outputId, 
                       local GModule      _module, 
                       local GProgram     _program, 
                       local String       _out, 
                       local String       _dstVar, 
                       local boolean      _bGLSL,
                       local PointerArray _parents
                       ) {

      float value = getFloatParamById("value");

      local GInput *input;

      // value
      local String *sV;
      input <= getInputById("value");
      if(input.isConnected())
      {
         sV <= _module.allocTmpVarScript(_out, TYPE_FLOAT, "in_v");
         input.emit(_module, _program, _out, sV, _bGLSL, _parents);
         _out.append(sV+" += "+value+";\n");
      }
      else
         sV <= String(value);

      _out.append(_dstVar+" = "+sV+";\n");
   }

}
