// ----
// ---- file   : ShaderVG_RectStrokeAA.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : Distributed under terms of the MIT license (https://opensource.org/licenses/MIT)
// ----          Copyright 2025 by bsp
// ----
// ----          Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
// ----          associated documentation files (the "Software"), to deal in the Software without restriction, including
// ----          without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// ----          copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to
// ----          the following conditions:
// ----
// ----          The above copyright notice and this permission notice shall be included in all copies or substantial
// ----          portions of the Software.
// ----
// ----          THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
// ----          NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// ----          IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
// ----          WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// ----          SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
// ----
// ---- info   : ShaderVG render utilities
// ----
// ----
// ----

module MShaderVG_RectStrokeAA;

namespace shadervg;


class RectStrokeAA : Shape {

   public method init() {
      Shape::init();
   }

   // ------------ vertex shader --------------
   static String vs_src =
      "uniform mat4 u_transform; \n"
      " \n"
      "ATTRIBUTE vec2 a_vertex; \n"
      " \n"
      "VARYING_OUT vec2 v_p; \n"
      " \n"
      "void main(void) { \n"
      "  v_p = a_vertex; \n"
      "  gl_Position = u_transform * vec4(a_vertex,0,1); \n"
      "} \n"
      ;

   // ------------ fragment shader ------------
   static String fs_src =
      "uniform vec2  u_center; \n"
      "uniform vec2  u_size_i; \n"
      "uniform vec2  u_size_o; \n"
      "uniform float u_aa_range; \n"
      "uniform float u_aa_exp; \n"
      "uniform vec4  u_color_stroke; \n"
      "uniform float u_debug; \n"
      " \n"
      "VARYING_IN vec2 v_p; \n"
      " \n"
      "void main(void) { \n"
      "  float aRectI = 0.0; \n"
      "  float aRectO = 0.0; \n"
      "  vec4 colorO = vec4(0,0,0,0); \n"
      " \n"
      "  // outer \n"
      "  vec2 vd = abs(v_p - u_center); \n"
   // // if(vd.x < u_size_o.x && vd.y < u_size_o.y)
      "  { \n"
      "    aRectO  = 1.0 - smoothstep(u_size_o.x-u_aa_range, u_size_o.x, vd.x); \n"
      "    aRectO *= 1.0 - smoothstep(u_size_o.y-u_aa_range, u_size_o.y, vd.y); \n"
      "   colorO = u_color_stroke; \n"
      "  } \n"
      " \n"
      "  // inner \n"
      "  vd = abs(v_p - u_center); \n"
      "  if(vd.x < u_size_i.x && vd.y < u_size_i.y) \n"
      "  { \n"
      "    aRectI  = 1.0 - smoothstep(u_size_i.x - u_aa_range, u_size_i.x, vd.x); \n"
      "    aRectI *= 1.0 - smoothstep(u_size_i.y - u_aa_range, u_size_i.y, vd.y); \n"
      "  } \n"
      " \n"
      "  float aI = 1.0 - aRectI; \n"
      "  float aO = aRectO; \n"
      " \n"
      "#if 1 \n"
      "  aI = pow(aI, u_aa_exp); \n"
      "#endif \n"
      "  vec4 color = vec4(colorO.xyz, colorO.a * aI); \n"
      " \n"
      "  // a = smoothstep(0.0, 1.0, a); \n"
      "#if 1 \n"
      "  aO = pow(aO, u_aa_exp); \n"
      "#endif \n"
      " \n"
      "  FRAGCOLOR = vec4(color.xyz, color.a*aO); \n"
      "  if(u_debug > 0.0) \n"
      "    FRAGCOLOR = vec4(1,0,0,1); \n"
      "} \n"
      ;

   public virtual onOpen() : boolean {
      if(createShapeShader(vs_src, fs_src))
      {
         return true;
      }
      return false;
   }

   public method setupRectStrokeAAVBO32(Buffer _vb, Buffer _dl,
                                        float _centerX, float _centerY,
                                        float _sizeX, float _sizeY,
                                        float _strokeW,
                                        float _aaRange
                                        ) {
      //  +0  u16 aaRange * 256
      //  +2  i32 vbOffBorder
      //  +6  u16 numVertsBorder
      //  +8  u16 primTypeBorder

      _dl.i16 = _aaRange * 256;

      boolean bSingle = ((_sizeX*_sizeY) <= RECT_SINGLE_AREA_THRESHOLD);
      int numVerts;

      if(bSingle)
      {
         numVerts = 4;

         _dl.i32 = _vb.offset;
         _dl.i16 = numVerts;
         _dl.i16 = GL_TRIANGLE_FAN;

         EmitQuadVertices(_vb,
                          _centerX - _sizeX - _strokeW,
                          _centerY - _sizeY - _strokeW,
                          (_sizeX + _strokeW) * 2.0f,
                          (_sizeY + _strokeW) * 2.0f
                          );
      }
      else // if(b_draw_border)
      {
         numVerts = 8*3;

         _dl.i32 = _vb.offset;
         _dl.i16 = numVerts;
         _dl.i16 = GL_TRIANGLES;

         EmitRectBorderVertices(_vb,
                                _centerX, _centerY,
                                _sizeX, _sizeY,
                                _strokeW,
                                _aaRange
                                );
      }

   }

   public method drawRectStrokeAAVBO32(int      _vboId,
                                       int      _byteOffsetBorder,
                                       int      _numVertsBorder,
                                       int      _glPrimTypeBorder,
                                       Matrix4f _projMatrix,
                                       float    _centerX, float _centerY,
                                       float    _sizeX,   float _sizeY,
                                       float    _strokeR, float _strokeG, float _strokeB, float _strokeA,
                                       float    _strokeW,
                                       float    _aaRange,
                                       float    _aaExp
                                       ) {

      ShaderVG.BindVBO(_vboId);

      // Outer border
      if(_numVertsBorder > 0)
      {
         shape_shader.bind();

         glUniformMatrix4fv(shape_u_transform, 1/*count*/, GL_TRUE/*transpose*/, _projMatrix);
         glUniform2f(shape_u_center,   _centerX, _centerY);
         glUniform2f(shape_u_size_i,   _sizeX - _strokeW, _sizeY - _strokeW);
         glUniform2f(shape_u_size_o,   _sizeX + _strokeW, _sizeY + _strokeW);
         glUniform1f(shape_u_aa_range, _aaRange);

         if(-1 != shape_u_aa_exp)
            glUniform1f(shape_u_aa_exp, _aaExp);

         glUniform4f(shape_u_color_stroke, _strokeR, _strokeG, _strokeB, _strokeA);

         if(-1 != shape_u_debug)
            glUniform1f(shape_u_debug, b_debug ? 1.0f : 0.0f);

         zglVertexAttribOffset(shape_a_vertex, 2/*size*/, GL_FLOAT, GL_FALSE/*normalize*/, 0/*stride*/, _byteOffsetBorder);
         glEnableVertexAttribArray(shape_a_vertex);

         glDrawArrays(_glPrimTypeBorder, 0, _numVertsBorder);

         glDisableVertexAttribArray(shape_a_vertex);
      }
   }

   public method drawRectStrokeAA(Buffer _scratchBuf,
                                  Matrix4f _projMatrix,
                                  float _centerX, float _centerY,
                                  float _sizeX,   float _sizeY,
                                  float _strokeR, float _strokeG, float _strokeB, float _strokeA,
                                  float _strokeW,
                                  float _aaRange,
                                  float _aaExp
                                  ) {

      ShaderVG.BindScratchBuffer();

      boolean bSingle = ((_sizeX*_sizeY) <= RECT_SINGLE_AREA_THRESHOLD);

      // Outer corners
      shape_shader.bind();

      glUniformMatrix4fv(shape_u_transform, 1/*count*/, GL_TRUE/*transpose*/, _projMatrix);
      glUniform2f(shape_u_center,   _centerX, _centerY);
      glUniform2f(shape_u_size_i,   _sizeX - _strokeW, _sizeY - _strokeW);
      glUniform2f(shape_u_size_o,   _sizeX + _strokeW, _sizeY + _strokeW);
      glUniform1f(shape_u_aa_range, _aaRange);

      if(-1 != shape_u_aa_exp)
         glUniform1f(shape_u_aa_exp, _aaExp);

      glUniform4f(shape_u_color_stroke, _strokeR, _strokeG, _strokeB, _strokeA);

      if(-1 != shape_u_debug)
         glUniform1f(shape_u_debug, b_debug ? 1.0f : 0.0f);

      glEnableVertexAttribArray(shape_a_vertex);

      if(bSingle)
      {
         setQuadVertices(shape_a_vertex, _scratchBuf,
                         _centerX - _sizeX - _strokeW,
                         _centerY - _sizeY - _strokeW,
                         (_sizeX + _strokeW) * 2.0f,
                         (_sizeY + _strokeW) * 2.0f
                         );
         glDrawArrays(GL_TRIANGLE_FAN, 0, 4);
      }
      else
      {
         allocScratchBuffer(shape_a_vertex, _scratchBuf, (8*3*2/*xy*/*4/*float*/));

         EmitRectBorderVertices(_scratchBuf,
                                _centerX, _centerY,
                                _sizeX, _sizeY,
                                _strokeW,
                                _aaRange
                                );

         glDrawArrays(GL_TRIANGLES, 0, 8*3);
      }

      glDisableVertexAttribArray(shape_a_vertex);
   }

}
