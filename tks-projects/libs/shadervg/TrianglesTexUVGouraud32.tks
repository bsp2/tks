// ----
// ---- file   : ShaderVG_TrianglesTexUVGouraud32.tks
// ---- author : Bastian Spiegel <bs@tkscript.de>
// ---- legal  : Distributed under terms of the MIT license (https://opensource.org/licenses/MIT)
// ----          Copyright 2025 by bsp
// ----
// ----          Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
// ----          associated documentation files (the "Software"), to deal in the Software without restriction, including
// ----          without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// ----          copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to
// ----          the following conditions:
// ----
// ----          The above copyright notice and this permission notice shall be included in all copies or substantial
// ----          portions of the Software.
// ----
// ----          THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
// ----          NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// ----          IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
// ----          WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// ----          SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
// ----
// ---- info   : ShaderVG render utilities
// ----
// ----
// ----

module MShaderVG_TrianglesTexUVGouraud32;

namespace shadervg;


class TrianglesTexUVGouraud32 : Shape {

   public method init() {
      Shape::init();
   }

   // ------------ vertex shader --------------
   static String vs_src =
      "uniform mat4 u_transform; \n"
      " \n"
      "ATTRIBUTE vec2 a_vertex; \n"
      "ATTRIBUTE vec2 a_uv; \n"
      "ATTRIBUTE vec4 a_color; \n"
      " \n"
      "VARYING_OUT vec2 v_uv; \n"
      "VARYING_OUT vec4 v_color; \n"
      " \n"
      "void main() { \n"
      "  gl_Position = u_transform * vec4(a_vertex,0,1); \n"
      "  v_uv = a_uv; \n"
      "  v_color = a_color; \n"
      "} \n"
      ;

   // ------------ fragment shader ------------
   static String fs_src =
      "uniform sampler2D u_sampler; \n"
      "uniform vec4      u_color_fill; \n"
      " \n"
      "VARYING_IN vec2 v_uv; \n"
      "VARYING_IN vec4 v_color; \n"
      " \n"
      "void main() { \n"
      "  FRAGCOLOR = TEXTURE2D(u_sampler, v_uv) * u_color_fill * v_color; \n"
      "} \n"
      ;

   protected virtual validateShapeShader() : boolean {
      return
         (-1 != shape_a_vertex)     &&
         (-1 != shape_a_uv)         &&
         (-1 != shape_u_transform)  &&
         (-1 != shape_u_sampler)    &&
         (-1 != shape_u_color_fill)
         ;
   }

   public virtual onOpen() : boolean {
      if(createShapeShader(vs_src, fs_src))
      {
         return true;
      }
      return false;
   }

   public method drawTrianglesTexUVGouraudVBO32(int      _vboId,
                                                int      _byteOffset,
                                                int      _numTris,
                                                Matrix4f _projMatrix,
                                                float    _fillR, float _fillG, float _fillB, float _fillA
                                                ) {
      // VBO vertex format (20 bytes per vertex):
      //     +0  f32 x
      //     +4  f32 y
      //     +8  f32 u
      //     +12 f32 v
      //     +16  u8 r
      //     +17  u8 g
      //     +18  u8 b
      //     +19  u8 a

      ShaderVG.BindVBO(_vboId);

      shape_shader.bind();

      glUniformMatrix4fv(shape_u_transform, 1/*count*/, GL_TRUE/*transpose*/, _projMatrix);
      glUniform4f(shape_u_color_fill, _fillR, _fillG, _fillB, _fillA);
      glUniform1i(shape_u_sampler, 0);

      zglVertexAttribOffset(shape_a_vertex, 2/*size*/, GL_FLOAT,         GL_FALSE/*normalize*/, 20/*stride*/, _byteOffset +  0);
      zglVertexAttribOffset(shape_a_uv,     2/*size*/, GL_FLOAT,         GL_FALSE/*normalize*/, 20/*stride*/, _byteOffset +  8);
      zglVertexAttribOffset(shape_a_color,  4/*size*/, GL_UNSIGNED_BYTE, GL_TRUE /*normalize*/, 20/*stride*/, _byteOffset + 16);

      glEnableVertexAttribArray(shape_a_vertex);
      glEnableVertexAttribArray(shape_a_uv);
      glEnableVertexAttribArray(shape_a_color);

      glDrawArrays(GL_TRIANGLES, 0, _numTris * 3);

      glDisableVertexAttribArray(shape_a_color);
      glDisableVertexAttribArray(shape_a_uv);
      glDisableVertexAttribArray(shape_a_vertex);
   }

}
