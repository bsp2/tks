
module MScrMapFailed;

namespace racer;

use tkopengl;


class ScrMapFailed : OSDScreen {

   OptionList opt;

   public method onDraw(float dt) {
      zglInit2D(Viewport.width, Viewport.height);
//       if(RacerConfiguration.b_motion_blur)
//       {
//          player.speed = 6;
//          MotionBlur.OnDraw();
//       }
      glClearColor(0,0,0.2,1);
      glClear(GL_COLOR_BUFFER_BIT);
      glLoadIdentity();

      glEnable(GL_TEXTURE_2D);
      tex_font.bind();
      glDisable(GL_BLEND);
      glDisable(GL_DEPTH_TEST);
      glDisable(GL_CULL_FACE);
      glEnable(GL_BLEND);
      glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);

      float vw = Viewport.width;
      float vh = Viewport.height;

      String txt = "- You failed to qualify -";
      float txtW = (txt.length-1) * 8;
      zglColorARGB(#ffaf3f3f);
      RenderUtils.DrawFixedText((vw - txtW)*0.5, (vh-8)*0.5, tex_font.sx, tex_font.sy, txt);

      opt.draw(dt);

      glDisable(GL_TEXTURE_2D);
      glDisable(GL_BLEND);
   }

   protected method handleOptSelection(int _id) {
      switch(_id)
      {
         case 0:
            scr_ingame.restartRace();
            OSD.ShowScreenCont(scr_ingame);
            break;

         case 1:
            OSD.ShowScreenCont(scr_ingame);
            replay.startReplay();
            break;
//          case 1:
//             OSD.ShowScreen(scr_map_select);
//             break;

         case 2:
            OSD.ShowScreen(scr_quit_to_title);
            break;
      }
   }

   public method onKey(Key _k) : boolean {
      switch(_k.pressed)
      {
         case VKEY_UP:
            opt.selectPrev();
            return true;

         case VKEY_DOWN:
            opt.selectNext();
            return true;

         case VKEY_RETURN:
         case VKEY_PAGEDOWN: // 'X' button on Pandora
            handleOptSelection(opt.sel_index);
            return true;
      }
      return false;
   }

   public method onActivate() { 
      opt.setOptions([
                        "Retry", // 0
                        "View replay", // 1
                        "Quit to title"  // 2
                        ]);
      onOpen();

      RacerConfiguration.b_ingame = false;

      if(RacerConfiguration.b_record)
      {
         RacerConfiguration.b_replay_fail = true;
         replay.stopRecord();
      }
   }

   public method onDeactivate() { 
   }

   public method onOpen() {
      opt.setGeometry((Viewport.width-20*8)*0.5 + 3*8, (Viewport.height-8)*0.5+6*8, 20*8, 4*8);
   }

}

ScrMapFailed  scr_map_failed;
