
module MRenderUtils;

namespace racer;

use tkopengl;

Texture tex_font;


class RenderUtils {

   static public OnOpen() {
      tex_font.unload();
      if(tex_font.loadImage("tex_font.png", 0,0,0))
      {
         trace "[dbg] RenderUtils::OnOpen: font texture loaded OK";
      }
      else
      {
         trace "[---] RenderUtils::OnOpen: failed to open font texture!";
      }
      tex_font.expand2n();
      tex_font.flags = TEX_MODULATE | TEX_ALPHA;
      tex_font.upload();
   }

   static method DrawDebugSphere(float px, py, pz, r) {
      int NUMSEG = 16;
      float a = 0;
      float b = 0;
      float w = 2PI/NUMSEG;
      glPointSize(3);
      glLineWidth(1);
      glBegin(GL_LINE_LOOP);
      compile loop(NUMSEG)
      {
         loop(NUMSEG)
         {
            float x = px + cos(a) * sin(b) * r;
            float y = py + sin(a) * sin(b) * r;
            float z = pz + cos(b) * r;
            glVertex3f(x, y, z);
            a += w;
         }
         b += w;
      }
      glEnd();
   }

   static method DrawFixedText(float _x, _y, int _fontW, int _fontH, String _text) {
      float cx, cy;

      if!(RacerConfiguration.b_multisampling)
      {
         _x += 0.35;
         _y += 0.35;
      }
      cx = _x;
      cy = _y;
      int idx = 0;
      float uStep = 8.0 / _fontW;
      float vTop = 0;//11 / float(_fontH);
      float vStep = 11.0 / _fontH;
      glBegin(GL_QUADS);
      loop(_text.length)
      {
         char c = _text.getc(idx++);
         if(c > ' ')
         {
            float u = (c-33) * uStep;
            glTexCoord2f (      u,  vTop);
            glVertex2f   (     cx,    cy);

            glTexCoord2f (u+uStep,  vTop);
            glVertex2f   (   cx+8,    cy);

            glTexCoord2f (u+uStep,  vTop + vStep);
            glVertex2f   (   cx+8, cy+11);

            glTexCoord2f (      u,  vTop + vStep);
            glVertex2f   (     cx, cy+11);

            cx += 8;
         }
         else if(c == '\n')
         {
            cx = _x;
            cy += 11;
         }
         else
         {
            cx += 8;
         }
      }
      glEnd();
   }

}
