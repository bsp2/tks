
module MScrIngameMenu;

namespace racer;

use tkopengl;


class ScrIngameMenu : OSDScreen {

   OptionList opt;

   public method onDraw(float dt) {
      zglInit2D(Viewport.width, Viewport.height);
      glClearColor(0,0,0.2,1);
      glClear(GL_COLOR_BUFFER_BIT);
      glLoadIdentity();

      glEnable(GL_TEXTURE_2D);
      tex_font.bind();
      glDisable(GL_BLEND);
      glDisable(GL_DEPTH_TEST);
      glDisable(GL_CULL_FACE);
      glEnable(GL_BLEND);
      glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);

      float vw = Viewport.width;
      float vh = Viewport.height;

      String txt = "- ingame options -";
      float txtW = (txt.length-1) * 8;
      zglColorARGB(#ffffffaf);
      RenderUtils.DrawFixedText((vw - txtW)*0.5, (vh-8)*0.5, tex_font.sx, tex_font.sy, txt);

      opt.draw(dt);

      glDisable(GL_TEXTURE_2D);
      glDisable(GL_BLEND);
   }

   protected method handleOptSelection(int _id) {
      switch(_id)
      {
         case 0:
            OSD.ShowScreenCont(scr_ingame);
            break;

         case 1:
            scr_ingame.restartRace();
            OSD.ShowScreenCont(scr_ingame);
            break;

         case 2:
            OSD.ShowScreenCont(scr_ingame);
            if(RacerConfiguration.b_record)
            {
               RacerConfiguration.b_replay_fail = true;
               replay.stopRecord();
            }
            replay.startReplay();
            break;

         case 3:
            OSD.ShowScreen(scr_editor);
            break;

         case 4:
            OSD.ShowScreen(scr_quit_to_title);
            break;
      }
   }

   public method onKey(Key _k) : boolean {
      switch(_k.pressed)
      {
         case VKEY_ESCAPE:
            OSD.ShowScreenCont(scr_ingame);
            return true;

         case VKEY_UP:
            opt.selectPrev();
            return true;

         case VKEY_DOWN:
            opt.selectNext();
            return true;

         case VKEY_RETURN:
         case VKEY_PAGEDOWN: // 'X' button on Pandora
            handleOptSelection(opt.sel_index);
            return true;
      }
      return false;
   }

   public method onActivate() { 
      opt.setOptions([
                        "Continue game", // 0
                        "Restart race",  // 1
                        "View replay",   // 2
                        "Edit map",      // 3
                        "Quit to title"  // 4
                        ]);
      onOpen();
   }

   public method onDeactivate() { 
   }

   public method onOpen() {
      opt.setGeometry((Viewport.width-20*8)*0.5 + 3*8, (Viewport.height-8)*0.5+6*8, 20*8, 8*8);
   }

}

ScrIngameMenu  scr_ingame_menu;
