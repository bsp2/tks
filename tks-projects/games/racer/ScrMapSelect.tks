
module MScrMapSelect;

namespace racer;

use tkopengl;


class ScrMapSelect : OSDScreen {

   OptionList opt;

   public method onDraw(float dt) {
      // Draw race time
      zglInit2D(Viewport.width, Viewport.height);
      glClearColor(0,0,0.2,1);
      glClear(GL_COLOR_BUFFER_BIT);
      glLoadIdentity();

      glEnable(GL_TEXTURE_2D);
      tex_font.bind();
      glDisable(GL_BLEND);
      glDisable(GL_DEPTH_TEST);
      glDisable(GL_CULL_FACE);
      glEnable(GL_BLEND);
      glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);

      float vw = Viewport.width;
      float vh = Viewport.height;

      String txt = "- Select map -";
      float txtW = (txt.length-1) * 8;
      zglColorARGB(#ffffffaf);
      RenderUtils.DrawFixedText((vw - txtW)*0.5, 40, tex_font.sx, tex_font.sy, txt);

      opt.draw(dt);

      glDisable(GL_TEXTURE_2D);
      glDisable(GL_BLEND);
   }

   protected method handleOptSelection(int _id) {
      switch(_id)
      {
         default:
            Editor.LoadTrackCycleList(_id);
            OSD.ShowScreen(scr_ingame);
            break;
      }
   }

   public method onKey(Key _k) : boolean {
      switch(_k.pressed)
      {
         case VKEY_ESCAPE:
            OSD.ShowScreen(scr_title);
            return true;

         case VKEY_UP:
            opt.selectPrev();
            return true;

         case VKEY_DOWN:
            opt.selectNext();
            return true;

         case VKEY_RETURN:
         case VKEY_PAGEDOWN: // 'X' button on Pandora
            handleOptSelection(opt.sel_index);
            return true;
      }
      return false;
   }

   public method onActivate() { 
      String mapName;
      StringArray opts; opts.empty();
      Integer mapIdx = 1;
      foreach mapName in mapcycle_list {
         opts.add("#"+(mapIdx.printf("%02d"))+" :  "+ mapName);
         mapIdx = mapIdx+1;
      }
      opt.setOptions(opts);
      
      SDL.enableKeyRepeat(200, 30);

      onOpen();
   }

   public method onOpen() {
      opt.setGeometry((Viewport.width-40*8)*0.5, 120, 13*8, (Viewport.height-130));
   }

   public method onDeactivate() { 
      SDL.enableKeyRepeat(0, 0);
   }

}

ScrMapSelect  scr_map_select;
