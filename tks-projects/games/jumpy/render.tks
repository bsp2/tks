
module Render;

namespace jumpy;

class Render {

   public static method drawFilledBox(float x,y,w,h,int c32) compile {
      zglColorARGB(c32);
      glBegin(GL_QUADS);
      glVertex2f(x,y);
      glVertex2f(x+w,y);
      glVertex2f(x+w,y+h);
      glVertex2f(x,y+h);
      glEnd();
   }

   public static method drawRotatedFilledBox(float x,y,w,h,int c32, float _a) compile {

      float lbx = -w * 0.5;
      float lby = -h * 0.5;

      float ltx = -w * 0.5;
      float lty =  h * 0.5;

      float rtx =  w * 0.5;
      float rty =  h * 0.5;

      float rbx =  w * 0.5;
      float rby = -h * 0.5;
      
      float ca = cos(_a);
      float sa = sin(_a);
      float tx, ty;

      zglColorARGB(c32);

      glBegin(GL_QUADS);

      tx = lbx * ca - lby * sa + x + w*0.5;
      ty = lbx * sa + lby * ca + y + h*0.5;
      glVertex2f(tx, ty);

      tx = ltx * ca - lty * sa + x + w*0.5;
      ty = ltx * sa + lty * ca + y + h*0.5;
      glVertex2f(tx, ty);

      tx = rtx * ca - rty * sa + x + w*0.5;
      ty = rtx * sa + rty * ca + y + h*0.5;
      glVertex2f(tx, ty);

      tx = rbx * ca - rby * sa + x + w*0.5;
      ty = rbx * sa + rby * ca + y + h*0.5;
      glVertex2f(tx, ty);

      glEnd();
   }

   public static method drawRectangle(float x,y,w,h,int c32) compile {
      zglColorARGB(c32);
      glPolygonMode(GL_FRONT, GL_LINE);
      glBegin(GL_QUADS);
      glVertex2f(x,y);
      glVertex2f(x+w,y);
      glVertex2f(x+w,y+h);
      glVertex2f(x,y+h);
      glEnd();
      glPolygonMode(GL_FRONT, GL_FILL);
   }

}
