
// 22Mar2010

module MWheelEntity;

namespace jumpy;


class WheelEntity extends BoxEntity {

   public float reset_speed;
   public float reset_angle;
   public StringArray members;

   float speed;
   float angle;

   public method initWheelEntity() {
      initEntity();
   }

   public virtual resetEntity() {
      Entity::resetEntity();

      speed = reset_speed;
      angle = reset_angle;
      b_visible = false;
   }

   public virtual calc(float dt) {
      if(b_active)
      {
         // Rotate
         angle = mathWrapf(angle + speed*dt, 0, 2PI);
         ////trace "xxx angle="+angle;
      }

      String *id;
      float a = angle;
      float w = (2PI / members.numElements);
      foreach id in members 
      {
         Entity e <= g_level.findEntityById(id);
         ////trace "xxx id="+id+" e="+#(e)+" members="+#(members);
         if(null != e)
         {
            float rx = cos(a) * size_x;
            float ry = sin(a) * size_y;
            float nx = position_x + rx - (e.size_x * 0.5);
            float ny = position_y + ry - (e.size_y * 0.5);
            e.delta_x    = nx - e.position_x;
            e.delta_y    = ny - e.position_y;
            e.position_x = nx;
            e.position_y = ny;
            e.gravity = 0;
         }
         a += w;
      }
   }

   public virtual draw(float dt) {
      return;
   }

   public virtual hitBy(Entity _o) : int {
      return 0;
   }

   public virtual actionSet(String key, value) : boolean {
      StringArray *a;
      switch(key)
      {
         case "angle":
            angle = mathClampf(value, 0, 360) * (2PI/360);
            return true;

         case "speed":
            speed = mathClampf(value, -20, 20) * (2PI/360);
            return true;
      }
      return Entity::actionSet(key, value);
   }

   public virtual markScriptTargets() {
      String *entityId;
      foreach entityId in members {
         Entity e <= g_level.findEntityById(entityId);
         if(null != e)
         {
            e.b_script_target = true;
         }
      }
   }

}
